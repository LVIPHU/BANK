USE [NganHang]
GO

/****** Object:  StoredProcedure [dbo].[SP_CHUYENCHINHANH_NV]    Script Date: 11/24/2021 12:12:01 PM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[SP_CHUYENCHINHANH_NV] @MANV nchar(10), @MACN nchar(10)
AS
DECLARE @LGNAME VARCHAR(50)
DECLARE @USERNAME VARCHAR(50)
SET XACT_ABORT ON;
SET TRANSACTION ISOLATION LEVEL SERIALIZABLE;
BEGIN TRY
	BEGIN TRAN
		IF EXISTS(SELECT * FROM LINK1.NganHang.dbo.NhanVien WHERE MANV = @MANV)
		BEGIN
			UPDATE LINK1.NganHang.dbo.NhanVien
			SET TrangThaiXoa = 0
			WHERE MANV = @MANV;
		END
		ELSE
		BEGIN
			INSERT INTO LINK1.NganHang.dbo.NhanVien (MANV, HO, TEN, DIACHI, PHAI, SODT, MACN, TRANGTHAIXOA)
			SELECT MANV, HO, TEN, DIACHI, PHAI, SODT, MACN = @MACN, TRANGTHAIXOA
			FROM dbo.NhanVien
			WHERE MANV = @MANV;
		END
		IF EXISTS(SELECT 1 FROM NhanVien
				WHERE NhanVien.MANV = @MANV AND				
				(EXISTS(SELECT 1 FROM GD_GOIRUT WHERE GD_GOIRUT.MANV = NhanVien.MANV) 
					OR EXISTS(SELECT MAGD FROM GD_CHUYENTIEN WHERE GD_CHUYENTIEN.MANV = NhanVien.MANV)))
		BEGIN 
			UPDATE dbo.NhanVien
			SET TrangThaiXoa = 1
			WHERE MANV = @MANV;
		END
		ELSE
		BEGIN
			DELETE FROM NhanVien Where NhanVien.MANV = @MANV
		END
		COMMIT TRAN;
		IF EXISTS(SELECT SUSER_SNAME(sid) FROM sys.sysusers WHERE name = CAST(@MANV AS NVARCHAR))
		BEGIN
		SET @LGNAME = CAST((SELECT SUSER_SNAME(sid) FROM sys.sysusers WHERE name = CAST(@MANV AS NVARCHAR)) AS VARCHAR(50))
		SET @USERNAME = CAST(@MANV AS VARCHAR(50))
		EXEC SP_DROPUSER @USERNAME;
		EXEC SP_DROPLOGIN  @LGNAME;
		END
END TRY
BEGIN CATCH
	IF (@@TRANCOUNT > 0)
	BEGIN
		ROLLBACK TRAN;
	END;
	THROW;
END CATCH

GO

